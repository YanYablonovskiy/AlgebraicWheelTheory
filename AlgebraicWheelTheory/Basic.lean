/-
Copyright (c) 2025 Yan Yablonovskiy. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yan Yablonovskiy
-/
import Mathlib.Tactic
import Mathlib.Algebra.Group.Defs
import Mathlib.Algebra.GroupWithZero.Defs
import Mathlib.Algebra.Ring.Defs
import Mathlib.Algebra.Field.Defs
/-!
# Algebraic wheels

Within this file , for a type `Î±` , an algebraic structure `Wheel Î±` is defined as a type-class.
Some straightforward results are provided given such a `Wheel Î±`.

## Main results

- `Wheel`: the definition of the wheel type-class
- `Wheel.instCommGroup` : the subgroup induced by the wheel.
- `Wheel.instSemiRing`  : the semiring induced by the wheel.

As well as all associated instances, and assorted wheel identities.

## Notation

 - `\â‚` : The notation for the wheel division unary operation, input as `\\â‚`.

## References

See JESPER CARLSTRÃ–M (2004) for the original account  `Wheels â€“ on division by zero`.
-/


/-- A Wheel is an algebraic structure which has two binary operations `(+,*)`, like a ring.
Similarly to a commutative ring, a Wheel is a commutative monoid in both operations. Additionally,
there is a multiplicative unary map `wDiv` which is an involution, as well as a few idiosyncratic
properties in the interactions of the `+`,`*` and `wDiv`.
-/
@[ext]
class Wheel.{u} (Î± : Type u) extends AddCommMonoid Î±, CommMonoid Î± where
/-- The map emulating division. -/
wDiv (a : Î±) : Î±
/-- This `wDiv` must be an involution -/
inv_wDiv: âˆ€ a : Î±, wDiv (wDiv a) = a
/-- This `wDiv` map is multiplicative. -/
wDiv_mul: âˆ€ a b : Î±, wDiv (a*b) = (wDiv a)*(wDiv b)
add_mul_wDiv: âˆ€ a b c: Î±, (a + b*c)*(wDiv b) = a*(wDiv b) + c + 0*b
right_mul_distrib: âˆ€ a b c: Î±, (a + b)*c + 0*c = a*c + b*c
right_mul_distrib': âˆ€ a b c: Î±, (a + 0*b)*c = a*c + 0*b
zero_mul_zero: (0:Î±)*0 = 0
div_add_zero: âˆ€ a b : Î±, wDiv (a + 0*b) = (wDiv a) + 0*b
wDiv_zero_add: âˆ€ a : Î±, 0*(wDiv 0) + a = 0*(wDiv 0)


open Wheel



universe u
variable {Î± : Type u} [W : Wheel Î±] (a b : Î±)

prefix:100  "\\â‚" => wDiv

attribute [simp] inv_wDiv wDiv_mul add_mul_wDiv zero_mul_zero div_add_zero wDiv_zero_add

/-- We have that `wDiv 1` is one, in general
-/
@[simp, grind]
lemma Wheel.wdiv_one : \â‚1 = (1:Î±) := by
  calc  \â‚ 1 = (1:Î±)* \â‚ 1 := by simp
          _   = \â‚\â‚ 1 * \â‚ 1 := by simp
          _   = \â‚ (\â‚ 1 * 1) := by rw [wDiv_mul]
          _   = \â‚ \â‚ 1  := by simp
          _   = 1 := by simp

/-- For any two terms `a b :Î±` with `[Wheel Î±]` , we have that `0*a + 0*b = 0*a*b`.
-/
@[simp, grind]
lemma Wheel.zero_mul_add : âˆ€a b: Î±, 0*a + 0*b = 0*a*b := by
 intro a b
 rw [add_comm,â†right_mul_distrib' 0 a b]
 simp

/-- For any `a :Î±` and `[Wheel Î±]` , `(0* \â‚ 0)*a = 0* \â‚ 0`. Morally, this is like
saying infinity times anything is infinity, complementing the axiom `wDiv_zero_add`.
-/
@[simp, grind]
lemma Wheel.zero_wdiv_mul : âˆ€a: Î±, (0* \â‚ 0)*a = 0* \â‚ 0 := by
  intro a
  rw [â†zero_mul_add,wDiv_zero_add]

/-- For any `a :Î±` and `[Wheel Î±]` , `a*\â‚a = 1 + 0*(a*\â‚a)` .
-/
@[simp, grind]
lemma Wheel.wdiv_self : âˆ€a: Î±, a*\â‚a = 1 + 0*(a*\â‚a) := by
  intro a
  have := add_mul_wDiv 0 (a:Î±) 1
  simp only [zero_add,mul_one] at this
  nth_rw 1 [this]
  rw [add_comm _ 1,add_assoc]
  suffices h: 0 * \â‚a + 0 * a = 0 * (a * \â‚a) by rw [h]
  simp
  rw [mul_assoc,mul_comm _ a]

/-- Defining the map from Wheels to its largest contained Semirings -/
@[reducible]
def ğ“¡ (Î± : Type u) [Wheel Î±] := {x : Î± // (0 : Î±) * x = 0}
/-- Defining the map from Wheels to its Group -/
@[reducible]
def ğ“¢ (Î± : Type u) [Wheel Î±] := {x : Î± // 0 * x = 0 âˆ§ 0 * \â‚x = 0}

@[reducible]
def ğ“¢' (Î± : Type u) [Wheel Î±] := {x : (ğ“¡ Î±) //  0 * \â‚(x.val) = 0}

@[reducible]
def ğ“¢toğ“¡ {Î± : Type u} [Wheel Î±] : (ğ“¢ Î±) â†’ (ğ“¡ Î±) := fun âŸ¨x,âŸ¨hx,_âŸ©âŸ© â†¦ âŸ¨x,hxâŸ©

@[reducible]
def ğ“¡toğ“¢' (Î± : Type u) [Wheel Î±] : (x:(ğ“¡ Î±)) â†’ (0 * \â‚(x.val) = 0) â†’ (ğ“¢' Î±) :=
 fun x hx â†¦ âŸ¨x,hxâŸ©

@[reducible]
def ğ“¡toğ“¢ {Î± : Type u} [Wheel Î±] : (x:(ğ“¡ Î±)) â†’ (0 * \â‚(x.val) = 0) â†’ (ğ“¢ Î±) :=
 fun x hx â†¦ âŸ¨x,âŸ¨x.prop,hxâŸ©âŸ©

instance [Wheel Î±] : Coe (ğ“¢ Î±) (ğ“¡ Î±) where
 coe := ğ“¢toğ“¡

instance [Wheel Î±] : Coe (ğ“¢ Î±) (ğ“¢' Î±) where
 coe := fun âŸ¨x,âŸ¨hxz,hxdivâŸ©âŸ© â†¦ âŸ¨âŸ¨x,hxzâŸ©,hxdivâŸ©

instance [Wheel Î±] : Coe (ğ“¢' Î±) (ğ“¢ Î±) where
 coe := fun âŸ¨âŸ¨x,hxzâŸ©,hxdivâŸ© â†¦ âŸ¨x,âŸ¨hxz,hxdivâŸ©âŸ©

/-- Addition instance for the induced semiring -/
instance : Add (ğ“¡ Î±) where
 add := fun a b â†¦ by
  refine âŸ¨a + b, ?_âŸ©
  calc 0*(â†‘a + â†‘b) = (a + b)*(0:Î±) + 0*0 := by rw [zero_mul_zero,add_zero,mul_comm]
  _ = 0 := by rw [right_mul_distrib,mul_comm,a.prop,mul_comm,b.prop,zero_add]

/-- Multiplication instance for the induced group -/
instance : Mul (ğ“¢ Î±) where
 mul := fun a b â†¦ by
  refine âŸ¨a*b, âŸ¨by rw [â†mul_assoc,a.prop.1,b.prop.1],?_âŸ©âŸ©
  rw [wDiv_mul,â†mul_assoc,a.prop.2,b.prop.2]

lemma mulğ“¢_def : âˆ€(a b : (ğ“¢ Î±)),a*b = âŸ¨a.val,a.propâŸ©*âŸ¨b.val,b.propâŸ© := fun a b â†¦ by rfl

lemma mulğ“¢_def' : âˆ€(a b : (ğ“¢ Î±)),a*b = âŸ¨a.val*b.val,(a*b).propâŸ© := fun a b â†¦ by rfl

instance : Inv (ğ“¢ Î±) where
 inv := fun a â†¦ âŸ¨\â‚a, âŸ¨a.prop.2,by rw [inv_wDiv,a.prop.1]âŸ©âŸ©

lemma invğ“¢_def : âˆ€(a : (ğ“¢ Î±)), aâ»Â¹ = âŸ¨\â‚â†‘a,aâ»Â¹.propâŸ© := fun a â†¦ by rfl

/-- Multiplication instance for the induced semiring -/
instance : Mul (ğ“¡ Î±) where
  mul := fun a b â†¦ by
   refine âŸ¨a * b, ?_âŸ©
   rw [â†mul_assoc,â†zero_mul_add,a.prop,b.prop,zero_add]

lemma mulğ“¡_def : âˆ€(a b : (ğ“¡ Î±)),a*b = âŸ¨a.val,a.propâŸ©*âŸ¨b.val,b.propâŸ© := fun a b â†¦ by rfl

lemma mulğ“¡_def' : âˆ€(a b : (ğ“¡ Î±)),a*b = âŸ¨a.val*b.val,(a*b).propâŸ© := fun a b â†¦ by rfl

lemma mulğ“¡_coe : âˆ€(a b : (ğ“¡ Î±)),a.val*b.val = (a*b).val := fun _ _ â†¦ rfl

/-- CommMagma instance for the multiplicative monoid -/
instance Wheel.instSCommMagma : CommMagma (ğ“¢ Î±) where
 mul_comm := fun a b â†¦ by
  ext
  convert W.mul_comm a b

/-- CommMagma instance for the multiplicative monoid -/
instance Wheel.instRCommMagma : CommMagma (ğ“¡ Î±) where
 mul := fun a b â†¦ a * b
 mul_comm := fun a b â†¦ by
  ext
  convert W.mul_comm a b

instance Wheel.instLeftDistrib : LeftDistribClass (ğ“¡ Î±) where
 left_distrib := by
  intro a b c;ext
  calc a * (b + c) = (b + c)*a + (0:Î±)*a := by rw [mul_comm,a.prop,add_zero]
  _ = a*b + a*c :=                      by simp_rw [right_mul_distrib,mul_comm]

/-- AddCommMagma instance for the additive monoid -/
instance Wheel.instAddCommMagma : AddCommMagma (ğ“¡ Î±) where
 add := fun a b â†¦ a + b
 add_comm := fun a b â†¦ by ext;convert W.add_comm a b

/-- AddCommMonoid instance for the AdditiveCommMonoid of the induced semiring TODO: GOLF -/
instance Wheel.instAddCommMonoid : AddCommMonoid (ğ“¡ Î±) where
 add_assoc := fun a b c â†¦ by
  ext
  convert W.add_assoc a b c
 zero := âŸ¨0,zero_mul_zeroâŸ©
 zero_add := fun a â†¦ by ext;convert W.zero_add a
 add_zero := fun a â†¦ by ext;convert W.add_zero a
 nsmul := fun n a â†¦ by
  refine âŸ¨n â€¢ a, ?_ âŸ©
  induction' n with m ih
  Â· rw [zero_nsmul,zero_mul_zero]
  Â· rw [succ_nsmul,mul_comm]
    calc ((m â€¢ a.val) + â†‘a) * 0 = (m â€¢ â†‘a + â†‘a) * 0 + 0*0 := by rw [zero_mul_zero,add_zero]
    _ =  (m â€¢ â†‘a) * 0 + â†‘a*0 := by rw [right_mul_distrib]
    _ = 0 := by  rw [mul_comm,ih,mul_comm,a.prop,zero_add]
 nsmul_succ := by intro n a; ext; convert W.nsmul_succ n a
 nsmul_zero := fun x â†¦ by ext; convert W.nsmul_zero x

/-- CommMonoid instance for the multiplicative CommMonoid of the induced semiring -/
instance Wheel.instRCommMonoid : CommMonoid (ğ“¡ Î±) where
 mul_assoc := fun a b c â†¦ by
  ext
  convert W.mul_assoc a b c
 one := âŸ¨1,mul_one 0âŸ©
 one_mul := fun a â†¦ by ext;convert W.one_mul a
 mul_one := fun a â†¦ by ext;convert W.mul_one a

/-- CommMonoid instance for the induced group. -/
instance Wheel.instSCommMonoid : CommMonoid (ğ“¢ Î±) where
 mul_assoc := fun a b c â†¦ by
  ext
  convert W.mul_assoc a b c
 one := âŸ¨1,âŸ¨mul_one 0,by rw [wdiv_one,mul_one 0]âŸ©âŸ©
 one_mul := fun a â†¦ by ext;convert W.one_mul a
 mul_one := fun a â†¦ by ext;convert W.mul_one a


/-- The commutative group corresponding to the "subset" of a wheel `[W:Wheel Î±]`:
 `{ w âˆˆ W |  0*w = 0 âˆ§ 0*\â‚w = 0}`. Note this is strictly not the case, as it is a subtype,
but its the analogous case. -/
instance Wheel.instCommGroup : CommGroup (ğ“¢ Î±) where
  inv_mul_cancel := fun a â†¦ by
   rw [invğ“¢_def,mul_comm,mulğ“¢_def']
   ext;congr
   rw [wdiv_self,â†mul_assoc,a.prop.1,a.prop.2,add_zero]


/-- The Semiring induced by a `[W:Wheel Î±]`,the corresponding to the "subset":
`{ w âˆˆ W |  0*w = 0}`. Note this is strictly not the case, as it is a subtype,
but its the analogous case. -/
instance Wheel.instSemiRing : Semiring (ğ“¡ Î±) where
  left_distrib := left_distrib
  right_distrib := fun a b c â†¦ by
    simp only [mul_comm,left_distrib]
  zero_mul := fun a â†¦ by
   ext; convert a.prop
  mul_zero := fun a â†¦ by ext; rw [mul_comm]; convert a.prop


/-- A predicate version when an explicit option is needed, without typeclass baggage. -/
def Wheel.toSemiring {Î± : Type u} [Wheel Î±] (hÎ± : âˆ€a:Î±, 0*a = 0): Semiring Î± := by
 have left_distrib: âˆ€(a b c:Î±),a*(b + c) = a*b + a*c := by
  intro a b c
  calc a * (b + c) = (b + c)*a + 0*a := by rw [mul_comm,hÎ± a,add_zero]
  _ = a*b + a*c :=                      by simp_rw [right_mul_distrib,mul_comm]
 exact {
 left_distrib := left_distrib
 right_distrib := fun a b c â†¦ by simp [mul_comm,left_distrib]
 zero_mul := hÎ±
 mul_zero := fun a â†¦ by rw [mul_comm,hÎ±]}



/-- For any `a b c :Î±` and `[Wheel Î±]` , ` a*c = b*c â†’ a + 0*c*\â‚c = b + 0*c*\â‚c `. This is the
version of cancellation that wheels enjoy.
-/
lemma Wheel.wdiv_right_cancel' : âˆ€a b c: Î±, a*c = b*c â†’ a + 0*c*\â‚c = b + 0*c*\â‚c := by
  intro a b c hab
  have: (a * c *\â‚c) = (b * c *\â‚c) := by rw [hab]
  rw [mul_assoc,mul_assoc,wdiv_self c,mul_comm,mul_comm b] at this
  rw [right_mul_distrib',right_mul_distrib'] at this
  simp only [one_mul,â†mul_assoc] at this
  exact this

/-- Since we have that `wDiv 1` is one, therefore `wDiv` is a Monoid Automorphism on `Î±`.
-/
instance Wheel.toMonoidHom : MonoidHom Î± Î± where
 toFun := wDiv
 map_one' := wdiv_one
 map_mul' := wDiv_mul

/-- If `c  :Î±` is a unit and `[Wheel Î±]` , then the inverse and Wheel self-division are related.
The predicate version -/
lemma Wheel.isUnit_add_eq_div_add' (c : Î±) (hc : IsUnit c):âˆƒb:Î±,c * b = 1 âˆ§ b * c = 1
 âˆ§ (b + (0:Î±)*\â‚c = \â‚c + 0*b) := by
 rw [isUnit_iff_exists] at hc
 obtain âŸ¨x,hx1,hx2âŸ© := hc
 use x
 refine âŸ¨hx1,hx2,?_âŸ©
 calc x + 0 * \â‚c = x*\â‚(x*c) + 0*\â‚c := by simp [hx2]
 _ = x*(\â‚x)*\â‚c + 0*\â‚c := by rw [wDiv_mul,â†mul_assoc]
 _ = \â‚c + 0*x*\â‚x + 0*\â‚c := by rw [wdiv_self,right_mul_distrib',one_mul,â†mul_assoc]
 _ = \â‚c + 0*x*\â‚x*\â‚c := by rw [add_assoc,mul_assoc 0,zero_mul_add]
 _ = \â‚c + 0*x := by rw [mul_assoc (0*x),â†wDiv_mul,hx2,wdiv_one,mul_one]

/-- If `c  :Î±` is a unit and `[Wheel Î±]` , then the inverse and Wheel self-division are related. -/
lemma Wheel.isUnit_add_eq_div_add (c : Î±Ë£) : (câ»Â¹ + (0:Î±)*\â‚â†‘c = \â‚â†‘c + 0*câ»Â¹) := by
 have: câ»Â¹ * c = (1:Î±) := by simp
 calc câ»Â¹ + (0:Î±) * \â‚â†‘c = câ»Â¹*\â‚(â†‘câ»Â¹*â†‘c) + 0*\â‚â†‘c := by simp
 _ = câ»Â¹*(\â‚â†‘câ»Â¹)*\â‚â†‘c + 0*\â‚â†‘c := by rw [wDiv_mul,â†mul_assoc]
 _ = \â‚â†‘c + 0*â†‘câ»Â¹*\â‚â†‘câ»Â¹ + 0*\â‚â†‘c := by rw [wdiv_self,right_mul_distrib',one_mul,â†mul_assoc]
 _ = \â‚â†‘c + 0*â†‘câ»Â¹*\â‚â†‘câ»Â¹*\â‚â†‘c := by rw [add_assoc,mul_assoc 0,zero_mul_add]
 _ = \â‚â†‘c + 0*â†‘câ»Â¹ := by rw [mul_assoc,â†wDiv_mul,this,wdiv_one,mul_one]


/-- If  `c  :Î±` is a unit and `[Wheel Î±]` , then we have that `0*\â‚c + 0*\â‚câ»Â¹ = 0` . -/
@[simp]
lemma Wheel.isUnit_zero_eq_div_mul_add (c : Î±Ë£) : (0:Î±)*\â‚â†‘c + (0:Î±)*\â‚â†‘câ»Â¹ = 0 := by
 rw [zero_mul_add,mul_assoc,â†wDiv_mul,show c * câ»Â¹ = (1:Î±) by simp,wdiv_one,mul_one]


/-- If  `c  :Î±` is a unit and `[Wheel Î±]`, then the inverse `câ»Â¹` is related to `\â‚c` as follows -/
lemma Wheel.isUnit_inv_eq_div_add (c : Î±Ë£) : câ»Â¹  = \â‚â†‘c + (0:Î±)*câ»Â¹*\â‚â†‘câ»Â¹ := by
 calc â†‘câ»Â¹ = â†‘câ»Â¹ + (0:Î±)*\â‚â†‘c +  0*\â‚â†‘câ»Â¹  := by rw [add_assoc,isUnit_zero_eq_div_mul_add,add_zero]
 _ =  (â†‘câ»Â¹ +  0*\â‚â†‘c) + (0:Î±)*\â‚â†‘câ»Â¹       := by rw [add_assoc]
 _ =  \â‚â†‘c + 0*câ»Â¹ + (0:Î±)*\â‚â†‘câ»Â¹           := by rw [isUnit_add_eq_div_add c]
 _ =  \â‚â†‘c + (0:Î±)*câ»Â¹*\â‚â†‘câ»Â¹               := by rw [add_assoc,zero_mul_add]


/-- If  `c  :Î±` is a unit and `[Wheel Î±]`, then the inverse `\â‚c` is further related to  `câ»Â¹` -/
lemma Wheel.isUnit_div_eq_inv_add (c : Î±Ë£) : \â‚â†‘c = câ»Â¹ + (0:Î±)*â†‘c*\â‚â†‘c := by
 calc \â‚â†‘c = \â‚â†‘c + (0:Î±)*â†‘câ»Â¹ +(0:Î±)*â†‘c := by simp [add_assoc,zero_mul_add]
 _ =  â†‘câ»Â¹ + 0 * \â‚â†‘c * â†‘c := by simp [â†isUnit_add_eq_div_add,add_assoc,zero_mul_add]
 _ =  câ»Â¹ + (0:Î±)*â†‘c*\â‚â†‘c :=  by simp only [mul_assoc,mul_comm]


/-- If `x : (ğ“¡ Î±)` is `\â‚`-invertible , then it is also part of (ğ“¢ Î±) -/
@[reducible]
def Wheel.isUnit_wdiv_coe (x : (ğ“¡ Î±)Ë£) (hinv : xâ»Â¹ = \â‚(x : Î±)) : (ğ“¢ Î±) := by
 refine âŸ¨ x, âŸ¨(x:ğ“¡ Î±).prop,?_âŸ©âŸ©
 calc (0:Î±) * \â‚â†‘â†‘x = (0*(â†‘x)) * \â‚â†‘â†‘x := by rw [(x:ğ“¡ Î±).prop]
 _ = 0 * (â†‘â†‘x * â†‘â†‘xâ»Â¹) := by rw [mul_assoc,â†hinv]
 _ = 0*â†‘â†‘(x*xâ»Â¹) := by rfl
 _ = 0 := by rw [(show â†‘â†‘(x * xâ»Â¹)=(1:Î±) by simp;congr),mul_one];


/-- If  (x : ğ“¡ Î±) is a unit, then it is a unit of the original wheel. -/
lemma Wheel.isRUnit_isUnit (x : ğ“¡ Î±) (hru : IsUnit x) : IsUnit (x:Î±) := by
 rw [isUnit_iff_exists] at *
 obtain âŸ¨y,hxy,hyxâŸ© := hru
 use y.val
 simp [mulğ“¡_coe,hxy,hyx];rfl


example : âˆ€(x y z: Î±),0*x + 0*y + 0*z + 0*z = 0*x*y*(z^2) := by
 intro x y z
 calc 0*x + 0*y + 0*z + 0*z = 0*(x*y) + 0*z + 0*z := by rw [zero_mul_add,mul_assoc]
 _ = 0*x*y*z + 0*z := by rw [zero_mul_add,â†mul_assoc]
 _ = 0*(x*y*z) + 0*z := by rw [mul_assoc,mul_assoc,â†mul_assoc x]
 _ = 0*(x*y*z)*z := by simp
 _ = 0*x*(y*z^2) := by rw [mul_assoc x,â†mul_assoc,mul_assoc (0*x),mul_assoc y,â†pow_two]
 _ = 0*x*y*(z^2) := by simp only [mul_assoc]
